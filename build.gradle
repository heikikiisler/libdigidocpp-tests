apply plugin: 'java'

repositories {
    jcenter()
}

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile group: 'org.testng', name: 'testng', version: '6.11'
    compile group: 'com.typesafe', name: 'config', version: '1.3.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
	compile group: 'io.appium', name: 'java-client', version: '5.0.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

}

def buildProperties = new Properties()
file("properties.conf").withInputStream {
    stream -> buildProperties.load(stream)
}

task prepareValidationZipFile(type: Zip) {
    description 'Generate zip file with data files for apps'
    from 'dataFiles'
    include '*'
    exclude '*.txt'
    exclude '*.zip'
    exclude '*.log'
    exclude '*.json'
    exclude '*.zip'
    exclude '*.tar'
    archiveName 'validationFiles.zip'
    destinationDir(file('download'))
    outputs.upToDateWhen {false}
}

def customSettings = {
    if (project.hasProperty('customResultsDir')) {
        long epoch = new Date().getTime() / 1000
        testReportDirName = "${buildDir}" + File.separator + epoch + "-" + project.property('customResultsDir')
        project.logger.lifecycle("[Gradle] Custom testReportDirName: " + testReportDirName)
    } else {
        project.logger.lifecycle("[Gradle] Default testReportDirName: " + testReportDirName)
    }
    if (project.hasProperty('resultsFile')) {
        systemProperty 'mobilecpp.results-file', project.property('resultsFile')
    }
}

task generateSuite(type: JavaExec) {
    description 'Generate TestNG testsuite based on list of data file names'
    configure customSettings
    classpath = sourceSets.test.runtimeClasspath
    main = "ee.testijad.mobilecpp.tests.GenerateSuite"
    outputs.upToDateWhen {false}
}

task validateWithAndroid(type: JavaExec) {
    description 'Android app downloads zip file from http server, validates data files and writes results back through the http server'
    doFirst {
        systemProperty 'mobilecpp.os', 'android'
    }
    classpath = sourceSets.main.runtimeClasspath
    main = "ee.testijad.mobilecpp.app.RunApps"
    outputs.upToDateWhen {false}
}

task validateWithIos(type: JavaExec) {
    description 'iOS app downloads zip file from http server, validates data files and writes results back through the http server'
    doFirst {
        systemProperty 'mobilecpp.os', 'ios'
    }
    classpath = sourceSets.main.runtimeClasspath
    main = "ee.testijad.mobilecpp.app.RunApps"
    outputs.upToDateWhen {false}
}

task validationTest(type: Test) {
    description 'Compares validation results with expected results. Execution based on TestNG testsuite file'
    doFirst {
        if (project.hasProperty('resultsFile')) {
            generateSuite.execute()
        }
    }
    configure customSettings
    useTestNG() {
        suites buildProperties.getProperty("testSuiteFileDirectory").replaceAll("\"", "")
        useDefaultListeners = true
    }
    outputs.upToDateWhen {false}
}

task androidTest(type: GradleBuild) {
    description 'Full Android app based workflow from data files to result comparison'
    tasks = ['prepareValidationZipFile', 'validateWithAndroid', 'generateSuite']
    doLast {
        configure customSettings
        validationTest.execute()
    }
}

task iosTest(type: GradleBuild) {
    description 'Full iOS app based workflow from data files to result comparison'
    tasks = ['prepareValidationZipFile', 'validateWithIos', 'generateSuite']
    doLast {
        configure customSettings
        validationTest.execute()
    }
}

task diagnosticHttpServer(type: JavaExec) {
    description 'For test http connectivity between mobile device and http server'
    classpath = sourceSets.main.runtimeClasspath
    main = "ee.testijad.mobilecpp.server.HttpServerRunner"
    outputs.upToDateWhen {false}
}

task wrapper(type: Wrapper) {
    description 'Gradle wrapper'
    gradleVersion = '4.3.1'
}
